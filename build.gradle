subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesBaseName = "MCStructs-bedrock-" + project.maven_name
    group = project.maven_group
    version = project.maven_version

    repositories {
        mavenCentral()
        maven {
            name = "lenni0451 releases"
            url = "https://maven.lenni0451.net/releases"
        }
    }

    dependencies {
        implementation "com.google.code.findbugs:jsr305:3.0.2"

        testImplementation platform("org.junit:junit-bom:5.9.0")
        testImplementation "org.junit.jupiter:junit-jupiter"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxParallelForks Runtime.runtime.availableProcessors()
    }

    publishing {
        repositories {
            maven {
                name = "reposilite"
                def releasesUrl = "https://maven.lenni0451.net/releases"
                def snapshotsUrl = "https://maven.lenni0451.net/snapshots"
                url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
            maven {
                name = "ossrh"
                def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId = project.maven_name
                groupId = project.maven_group
                version = project.maven_version

                from components.java

                pom {
                    name = rootProject.name
                    description = "A minecraft bedrock data structure library with the goal of being compliant with the vanilla client"
                    url = "https://github.com/Lenni0451/MCStructs-bedrock"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/Lenni0451/MCStructs-bedrock/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "Lenni0451"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/Lenni0451/MCStructs-bedrock.git"
                        developerConnection = "scm:git:ssh://github.com/Lenni0451/MCStructs-bedrock.git"
                        url = "github.com/Lenni0451/MCStructs-bedrock"
                    }
                }
            }
        }
    }

    signing {
        sign configurations.archives
        sign publishing.publications.maven
    }

    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }
    build.dependsOn(test)
}
